plugins {
    id 'org.springframework.boot' version '2.1.5.RELEASE'
    id 'java'
}


apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'maven-publish'

group = 'com.github.zw201913'
version = '1.0.4.RELEASE'
sourceCompatibility = '8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = "javadoc"
}

javadoc {
    options{
        encoding "UTF-8"
        charSet "UTF-8"
    }
}

publishing{
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version "${version}"
            from components.java
            artifact sourcesJar
            artifact javadocJar

            // https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
            pom {
                name = "quartz-cluster"
                description = "extended quartz distributed timing tasks"
                url = "https://github.com/zw201913/quartz-cluster"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "zw201913"
                        name = "Wei Zou"
                        email = "zou.wei930526@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/zw201913/quartz-cluster.git"
                    developerConnection = "scm:git:https://github.com/zw201913/quartz-cluster.git"
                    url = "https://github.com/zw201913/quartz-cluster"
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            // 如果version以SNAPSHOT结尾就会上传到快照仓库，如果不是就上传到release仓库
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = NEXUS_USERNAME
                password = NEXUS_PASSWORD
            }
        }

    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-quartz"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'com.google.guava:guava:27.1-jre'
}
